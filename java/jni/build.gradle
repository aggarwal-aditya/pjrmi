/*
 * The Gradle build file for deshaw-pjrmi.
 */

plugins {
    id 'cpp-library'
}

/*
 * Configuration from the outside world.
 */
def JAVA_HOME = System.properties['java.home']

configurations {
    jniHeaders {
        description = 'JNI headers required to compile the library'
        attributes {
            attribute(OperatingSystemFamily.OPERATING_SYSTEM_ATTRIBUTE, objects.named(OperatingSystemFamily, OperatingSystemFamily.LINUX))
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, 'jni-headers'))
        }
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    jniHeaders project(':java')
}

tasks.withType(CppCompile).configureEach {
    compilerArgs.add '-std=c++11'
    includes configurations.jniHeaders + files([JAVA_HOME + '/include', JAVA_HOME + '/include/linux'])
}

library {
    // Set the target operating system and architecture for this library
    targetMachines.add(machines.linux.x86_64)

    // The library name is libpjrmijni.so
    baseName = 'pjrmijni'

    dependencies {
        implementation project(":cpp")
    }
}